@INCLUDE "../StringLib/StringUtils.hws"
@INCLUDE "../mimetypes.hws"

; NOTE: 'default' is a hollywood keyword. Renamed to 'defalt' to prevent conflicts. This may change.
Global Files = {}
Files.location = Nil
Files.defalt = Nil

Function Files:New(options)
    options = IIf(GetType(options) = #TABLE, options, {})
    Local plugin = CopyTable(self)

    Local location = StringUtils.EMPTY
	If GetType(RawGet(options, "location")) = #STRING Then location = RawGet(options, "location")
	
    If StringUtils.Substring(location, 1, 2) <> "./"
        ; Make sure it's a relative path, forecefully!
        If StringUtils.Substring(location, 1, 1) = "/"
            location = "." .. location
        Else
            location = "./" .. location
        EndIf
    EndIf

    If StringUtils.Substring(location, -1, -1) = "/"
        location = StringUtils.Substring(location, 1, -2)
    EndIf

    plugin.location = location   ; this is now a relative path, without trailing slash

    Local defalt = IIf(GetType(RawGet(options, "defalt")) = #STRING, RawGet(options, "defalt"), "index.html")
    If Not StringUtils.IsEmpty(defalt)
        If StringUtils.Substring(defalt, 1, 1) <> "/"
            defalt = "/" .. defalt
        EndIf
    EndIf

    plugin.defalt = defalt    ; this is now a filename prefixed with a slash or ""
    Return (plugin)
EndFunction

Function Files:NewRequestResponse(request, response)
    Local stop = False
    Local method = request:Method()
    If method <> "GET" And method <> "HEAD"
        Return (stop)
    EndIf

    Local path = request:Path()
    If path = "/"
        If Not StringUtils.IsEmpty(self.defalt)
            response:Redirect(self.defalt)
            stop = True
        EndIf
        Return (stop)  ; default not set, so nothing to serve
    EndIf

    Local filename = self.location .. path

    Local type = MimeTypes.Guess(filename)
    stop = Not Not response:WriteFile(filename, IIf(IsNil(type), "text/html", type))
    Return (stop)
EndFunction
